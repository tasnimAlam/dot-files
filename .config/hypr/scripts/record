#!/usr/bin/env fish

if pgrep wf-recorder >/dev/null
    kill $(pgrep -x wf-recorder)
    return 1
end

# Set up default file save path and options
set output_dir ~/Videos
mkdir -p $output_dir
set filename "$output_dir/Rec-"(date +%I:%M:%S%p-%Y-%m-%d)".mp4"

# Prompt for recording mode using bemenu
set mode (echo -e "Full Screen\nSelect Area" | bemenu -i -p "Screen Recording Mode:")

# Check the selected mode and record accordingly
switch "$mode"
    case "Full Screen"
        wf-recorder -f "$filename"

    case "Select Area"
        # Use slurp to select a specific area
        set geometry (slurp)
        if test -n "$geometry"
            wf-recorder -g "$geometry" -f "$filename"
        else
            echo "No area selected. Exiting."
            exit 1
        end

    case '*'
        echo "Recording canceled."
        exit 1
end

notify-send "Screen Recording" "Recording saved as $filename"
